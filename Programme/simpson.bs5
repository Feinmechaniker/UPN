# Programm simpson.bor
# Simpsonsche Regel zum berechen bestimmter Integrale
# basiert auf dem Beispiel bei Kreul
#
# f(x) als Unterprogramm ab 100
# start 000, dann Eingabe untere und obere Grenze, RUN
# Anzeige des bestimmten Integrals
#
# Verwendete Speicher: 0 1 2 3 4 5 6 7 8
# 00 - a - untere Grenze
# 01 - b - obere Grenze
# 08 - m - streifenanzahl (20) besser direkt als Loop8
# 03 - i - Index, bei geradem i wird mit 2, bei ungeradem mit 4 multipliziert
# 04 - Faktor
# 05 - h - Schrittweite
# 06 - x
# 07 - S
000 2           ; Initialisierung, wir geben m als 20 vor
    0
    STO 8
    1
    STO 3
    HALT         ; Eingabe untere und obere grenze in y und x
    STO 1
    X<->Y
    STO 0        ; alles fertig
    -
    RCL 8
    /
    STO 5         ; Schrittweite
    RCL 8
    1
    -
    STO 8
    RCL 0
    STO 6         ; x = a
    GOSUB 100     ; Call f(x) fuer ersten Summand
    STO 7         ; S = f(x)
021 RCL 5         ; Schleife
    STO+ 6        ; x=x+h
    2
    RCL 3
    IF X=Y 31    ; if i = 2
    2
    STO 3
    2
    *             ; Ungerade, 4
    GOTO 034  	
031 1
    STO 3
    2
034 STO 4
    RCL 6
    GOSUB 100     ; f(x)
    ENTER
    RCL 4
    *
    STO+ 7
    LOOP8 21
    RDN
    RCL 7
    X<->Y
    -
    RCL 5
    *
    3
    /
    HALT
    GOTO 000
052 END 52

